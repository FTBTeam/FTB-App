import cz.habarta.typescript.generator.gradle.GenerateTask
import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import org.apache.tools.ant.filters.ReplaceTokens

import java.time.Instant

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.palantir.git-version" version "3.0.0"
    id "com.github.hierynomus.license-report" version "0.16.1"
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.8"
    id 'cz.habarta.typescript-generator' version "3.2.+"
}

apply plugin: com.hierynomus.gradle.license.LicenseReportingPlugin

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

group = "net.creeperhost"
archivesBaseName = "launcher"
def details = versionDetails()
def branch = System.getenv("BRANCH_OVERRIDE") ?: System.getenv("GITHUB_BRANCH") ?: "release" //(details.branchName ?: System.getenv("GITHUB_BRANCH") ?: "release").replaceAll('/', '-')
version = System.getenv("VERSION_OVERRIDE") ?: (details.gitHash + (branch != 'release' ? "-" + branch : ""))
def sentryDsn = System.getenv("SUBPROCESS_SENTRY_DSN") ?: ""

def packageJson = new JsonSlurper().parse(file("../package.json"))

repositories {
    mavenLocal()
    maven { url 'https://maven.creeperhost.net' }
    mavenCentral()
    jcenter() // Gradle _Says_ it wont resolve artifacts, but its gone into read-only indefinitely. (JdkUtils uses some libraries from here)
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-jul:2.17.2'
    implementation 'io.sentry:sentry-log4j2:5.7.4'

    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.nanohttpd:nanohttpd-websocket:2.3.1'
    implementation 'com.github.oshi:oshi-core:5.7.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.9.3'
    implementation 'org.apache.commons:commons-compress:1.2'
    implementation 'commons-io:commons-io:2.11.0'
    implementation('org.apache.maven:maven-artifact:3.8.4') {
        exclude group: 'org.codehaus.plexus' // Not needed for this application.
    }
    implementation 'net.covers1624:Quack:0.4.9.77'
    implementation 'net.covers1624:JdkUtils:0.4.0.14'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.openjdk.nashorn:nashorn-core:15.3'

    implementation 'com.electronwill.night-config:toml:3.6.4'

    implementation 'org.apache.httpcomponents:httpmime:4.5.13'

    implementation('software.amazon.awssdk:s3:2.20.4') {
        exclude group: 'software.amazon.awssdk', module: 'netty-nio-client'
    }

    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

//    if (file('debugger').exists()) {
//        runtimeOnly project(":debugger")
//    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
}

test {
    useJUnitPlatform()
}

tasks.register('processSource', Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [APPVERSION: version, WEBVERSION: version, SHAREDVERSION: packageJson.version, BRANCH: branch, SENTRY: sentryDsn])
    into "$buildDir/src"
}

compileJava {
    options.encoding = "UTF-8"
    dependsOn processSource
    source = processSource.outputs
}

jar.enabled = false
shadowJar {
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
    manifest {
        attributes([
                "Specification-Title"   : "Launcher",
                "Specification-Vendor"  : "Creeperhost LTD",
                "Specification-Version" : version,
                "Implementation-Title"  : "CreeperLauncher",
                "Implementation-Version": version,
                "Implementation-Vendor" : "Creeperhost LTD"
        ] as LinkedHashMap, "net/creeperhost/creeperlauncher/")
        attributes 'Multi-Release': 'true'
        attributes 'Main-Class': 'net.creeperhost.creeperlauncher.CreeperLauncher'
    }
}
assemble.dependsOn 'shadowJar'

// TODO make root dir a bare gradle project and subprocess the submodule
project(":web") {
}

downloadLicenses {
    // Can only be the debugger, just ignore
    includeProjectDependencies = false

    // Only include things that will be part of the final jar.
    dependencyConfiguration = "runtimeClasspath"
}

def dependencySources = layout.projectDirectory.file('dependency_sources.json')
def licensesOutput = layout.buildDirectory.file('libs/java-licenses.json')
def writeLicenses = tasks.register('writeLicenses') {
    it.dependsOn 'downloadLicenses'
    inputs.file(dependencySources)
    outputs.file(licensesOutput)

    // Parse the dependency-license.json and add the repository to the object where possible.
    doLast {
        def sources = new JsonSlurper().parse(dependencySources.asFile) as Map<String, String>
        def data = new JsonSlurper().parse(new File("$buildDir/reports/license/dependency-license.json"))
        def missing = []
        def used = []
        for (obj in data['dependencies']) {
            def name = obj['name']
            if (!name.contains(':')) continue
            name = name.take(name.lastIndexOf(":"))
            def repo = sources[name]
            used << name
            if (repo == null) {
                missing << name
            } else {
                obj.repository = repo
            }
        }
        if (!missing.isEmpty()) {
            getLogger().warn("The following dependencies are missing repo links: ")
            missing.each{ getLogger().warn("  $it")}
        }

        def unused = sources.keySet() - used
        if (!unused.isEmpty()) {
            getLogger().warn("The following dependency repo links aren't required: ")
            unused.each{ getLogger().warn("  $it")}
        }


        // Write the new json.
        licensesOutput.get().asFile.text = JsonOutput.toJson(data)
    }
}

def metaOutput = layout.buildDirectory.file('libs/meta.json')
def writeVersion = tasks.register('writeVersion') {
    outputs.file(metaOutput)
    doLast {
        metaOutput.get().asFile.text = JsonOutput.toJson(
                [
                        appVersion: packageJson.version,
                        commit    : details.gitHash,
                        branch    : branch,
                        released  : Instant.now().getEpochSecond(),
                        runtime   : [
                                version: project.target_java_version,
                                // Get the output of the fatjar -all
                                jar    : shadowJar.outputs.files.singleFile.name,

                                /**
                                 * The env and args use the following format:
                                 * {
                                 *    "key": "" // Optional, if not set, the value will be used as the key and value.
                                 *    "value": "",
                                 *    // At least one of the following must be set.
                                 *    "filter": {
                                 *       "os": "windows", // optional
                                 *       "arch": "x64", // optional
                                 *    }
                                 * }
                                 */
                                env    : [],
                                jvmArgs: []
                        ]
                ]
        )
    }
}

tasks.named('build').configure {
    it.dependsOn writeLicenses
    it.dependsOn writeVersion
}

wrapper {
    gradleVersion = "8.7"
}

idea.project.settings {
    delegateActions {
        // Gradle runners are garbage and break frequently.
        delegateBuildRunToGradle = false
        testRunner = org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner.PLATFORM
    }
    runConfigurations {
        "Start Backend"(org.jetbrains.gradle.ext.Application) {
            mainClass = "net.creeperhost.creeperlauncher.CreeperLauncher"
            moduleName = "creeperlauncher.main"
            jvmArgs = '-ea '                                             // Enable assertions
            jvmArgs += '-DDownloadTask.debug=true '                      // Enable Quack DownloadAction logging from DownloadTask
            jvmArgs += '-DInstanceLauncher.ProgressTracker.debug=true '  // Enable debug logging for InstanceLauncher's ProgressTracker TODO Log4j markers?
            jvmArgs += '-DInstanceInstaller.debug=true '                 // Enable debug logging for InstanceInstaller.
            jvmArgs += '-DInstanceLauncher.minecraft=ACCEPT '            // Allow Minecraft logging Marker in Log4j.
            jvmArgs += '-DInstanceShareUploader.debug=true '             // Enable debug logging for InstanceShareUploader
            jvmArgs += '-DCloudSaveManager.debug=true '                  // Enable debug logging for CloudSaveManager
            programParameters = '--dev'
        }
    }
}

def sharedSettings = [
        excludeClasses    : [
                "java.lang.Iterable",
                "java.lang.Comparable",
                "java.lang.Cloneable",
                "java.nio.Watchable",
                "java.io.Serializable",
                "com.google.common.hash.HashCode",
                "com.google.gson.JsonElement",
                "com.google.gson.JsonArray",
                "com.google.gson.JsonObject",
                "java.awt.geom.Dimension2D"
        ],
        customTypeMappings: [
                "java.nio.file.Path:string"
        ]
]

tasks.named("generateTypeScript", GenerateTask) {
    jsonLibrary = "gson"
    noFileComment = true
    noTslintDisable = true
    noEslintDisable = true
    tsNoCheck = true
    outputKind = "module"
    outputFile = project.file("../src/core/@types/javaApi.d.ts")
    classPatterns = [
            'net.creeperhost.creeperlauncher.api.handlers.instances.InstalledInstancesHandler$SugaredInstanceJson',
            'net.creeperhost.creeperlauncher.api.data.*',
            'net.creeperhost.creeperlauncher.api.data.**.*',
            'net.creeperhost.creeperlauncher.api.data.**.*$*',
            'net.creeperhost.creeperlauncher.api.handlers.**.*$Request',
            'net.creeperhost.creeperlauncher.api.handlers.**.*$Reply',
            'net.creeperhost.creeperlauncher.api.handlers.**.*$Data',
            'net.creeperhost.creeperlauncher.storage.settings.SettingsData'
    ]
    excludeClasses = sharedSettings.excludeClasses
    customTypeMappings = sharedSettings.customTypeMappings
    customTypeNamingFunction = """
(name, simpleName) => {
    const prefixTargets = ["Reply", "Progress", "Request", "Data"]
    if (prefixTargets.includes(simpleName)) {
        const targetEnding = prefixTargets[prefixTargets.indexOf(simpleName)];
        const reply = name.split(".").slice(-1)[0];
        const replyClassContainer = reply.split("\$")[0];
        return replyClassContainer + targetEnding;
    } 

    return simpleName;
}
"""
}

//tasks.register("generateTypeScriptExtras", GenerateTask) {
//    jsonLibrary = "gson"
//    outputKind = "module"
//    nullableAnnotations = ["org.jetbrains.annotations.Nullable"]
//    outputFile = project.file("../src/core/@types/javaDataTypes.d.ts")
//    excludeClasses = sharedSettings.excludeClasses
//    customTypeMappings = sharedSettings.customTypeMappings
//    classes = [
//            "net.creeperhost.creeperlauncher.data.InstanceJson",
//    ]
//}

//generateTypeScript.finalizedBy(generateTypeScriptExtras)
